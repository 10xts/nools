define Fibonacci {
            "value":-1,
            "sequence":null
        }
define Result {
    value : -1
}

rule Recurse {
    priority:1,
    when {
            not(f : Fibonacci f.sequence == 1);
            f1 : Fibonacci f1.sequence != 1;
    }
    then {
        assert(new Fibonacci({sequence : f1.sequence - 1}));
    }
}

rule Bootstrap {
   when {
       f : Fibonacci f.value == -1 && (f.sequence == 1 || f.sequence == 2);
   }
   then{
       modify(f, function(){
           this.value = 1;
       });
   }
}

rule Calculate {
    when {
        f1 : Fibonacci f1.value != -1 {sequence : s1};
        f2 : Fibonacci f2.value != -1 && f2.sequence == s1 + 1 {"sequence":"s2"};
        f3 : Fibonacci f3.value == -1 && f3.sequence == s2 + 1;
        r : Result
    }
    then {
        modify(f3, function(){
            this.value = r.result = f1.value + f2.value;
        });
        retract(f1);
    }
}
